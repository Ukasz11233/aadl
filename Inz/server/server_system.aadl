package Server_System
public
	with DataTypes;	
	renames DataTypes::all;
	
	with Buses;
	
	with Server_Software;
	renames Server_Software::all;
	
	with Server_Hardware;
	renames Server_Hardware::all;
	
	
	system Server_System
		features
			measured_data_in : in data port MeasuredData;
			wifi : requires bus access Buses::WiFi;
		flows
			mqtt_sink : flow sink measured_data_in;
	end Server_System;
	
	system implementation Server_System.impl
		subcomponents
			mqtt_process : process MainProcess.impl;
			db_process : process  DataBaseProcess.impl;
			
			pcie : bus Buses::PCIe;
			cpu : processor IntelXeon;
			ram: memory RAM;
			hdd : memory HDD;
			
		connections
			C1 : port measured_data_in -> mqtt_process.data_in;
			C2: port mqtt_process.data_out -> db_process.data_in;
		
			B1 : bus access pcie <-> cpu.pcie;
			B2 : bus access pcie <-> ram.pcie;
			B3 : bus access pcie <-> hdd.pcie;
			
		properties
			Actual_Processor_Binding => (reference(cpu)) applies to mqtt_process,
																	db_process;
			
			Actual_Connection_Binding => (reference(pcie)) applies to C1;
			Actual_Connection_Binding => (reference(pcie)) applies to C2;
			
	end Server_System.impl;	
	
end Server_System;
