package Server_System
public
	with DataTypes;	
	renames DataTypes::all;
	
	with SEI;
	
	with Buses;
	
	with Server_Software;
	renames Server_Software::all;
	
	with Server_Hardware;
	renames Server_Hardware::all;
	
	
	system Server_System
		features
			measured_data_in : in data port MeasuredData;
			api_data_out : out data port ProcessedData;
			wifi : requires bus access Buses::WiFi;
		flows
			mqtt_sink : flow sink measured_data_in;
			api_src : flow source api_data_out;
	end Server_System;
	
	system implementation Server_System.impl
		subcomponents
			mqtt_process : process MainProcess.impl;
			db_process : process  DataBaseProcess.impl;
			api_process : process ApiProcess.impl;
			
			pcie : bus Buses::PCIe;
			cpu : processor IntelXeon;
			ram: memory RAM;
			hdd : memory HDD;
			
		connections
			C1 : port measured_data_in -> mqtt_process.data_in;
			C2: port mqtt_process.data_out -> db_process.data_in;
			C3: port db_process.data_out -> api_process.data_in;
			C4: port api_process.data_out -> api_data_out;
		
			B1 : bus access pcie <-> cpu.pcie;
			B2 : bus access pcie <-> ram.pcie;
			B3 : bus access pcie <-> hdd.pcie;
			
		properties
			Actual_Processor_Binding => (reference(cpu)) applies to mqtt_process,
																	db_process,
																	api_process;
																	
			Actual_Memory_Binding => (reference(ram), reference(hdd)) applies to cpu, 
																	mqtt_process, 
																	mqtt_process.ReceiveThread,
																	mqtt_process.StoreThread, 
																	db_process,
																	api_process;
			
			
			Actual_Connection_Binding => (reference(pcie)) applies to B1;
			Actual_Connection_Binding => (reference(pcie)) applies to B2;
			Actual_Connection_Binding => (reference(pcie)) applies to B3;
			
			SEI::BandWidthBudget => 8.0 GBytesps applies to pcie;
			
			SEI::BandWidthBudget => 2.0 GBytesps applies to B1;
			SEI::BandWidthBudget => 1.0 GBytesps applies to B2;
			SEI::BandWidthBudget => 1.0 GBytesps applies to B3;	
			
			
			SEI::PowerCapacity => 0.8 KW;
			
	end Server_System.impl;	
	
end Server_System;
