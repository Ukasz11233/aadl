package STM32_Controller
public
	with DataTypes;
	renames DataTypes::all;

	with Buses;
	with SEI;
	

system STM32_Controller_System
	features
		measured_data : out data port MeasuredData;
		wifi : requires bus access Buses::WiFI;
	flows
		mqtt_src : flow source measured_data;
end STM32_Controller_System;

system implementation STM32_Controller_System.impl
	subcomponents
		-- software
		main_process : process MainProcess.with_thread;

		-- hardware ---
		arm_processor : processor Arm_CortexM;
		ahb_bus : bus AHB;
		flash_mem : memory InternalMemory.Flash;
		sram_mem : memory InternalMemory.Sram;
		co2_sensor : device CO2Sensor;
		buzzer : device Buzzer;
		led: device Led;
		spi_bus : bus SPI;
				
	connections
		C1 : port co2_sensor.co2_data_out -> main_process.co2_data_in;
		C2 : port main_process.buzzer_sig -> buzzer.turn_on_off;
		C3 : port main_process.led_sig -> led.turn_on_off;
		C4 : port main_process.measured_data -> measured_data;
		
		B1 : bus access ahb_bus <-> arm_processor.ahb;
		B2 : bus access ahb_bus <-> flash_mem.ahb;
		B3 : bus access ahb_bus <-> sram_mem.ahb;
		
		B4 : bus access spi_bus <-> co2_sensor.spi;
		B5 : bus access spi_bus <-> buzzer.spi;
		B6 : bus access spi_bus <-> led.spi;
										
	flows
		F1: end to end flow co2_sensor.co2_src -> 
										C1 ->
										main_process.buzzer_flow ->
										C2 ->
										buzzer.co2_sink;
										
		F2: end to end flow co2_sensor.co2_src ->
									C1 ->
									main_process.led_flow ->
									C3 ->
									led.co2_sink;																					
	properties
		Actual_Processor_Binding => (reference(arm_processor)) applies to main_process.buzzer_thread;
		Actual_Processor_Binding => (reference(arm_processor)) applies to main_process.led_thread;
		Actual_Processor_Binding => (reference(arm_processor)) applies to main_process.mqtt_thread;
		
		Actual_Memory_Binding => (reference(arm_processor)) applies to flash_mem;
		Actual_Memory_Binding => (reference(arm_processor)) applies to sram_mem;	
		
		Actual_Connection_Binding => (reference (ahb_bus)) applies to B1;
		Actual_Connection_Binding => (reference(ahb_bus)) applies to B2;
		Actual_Connection_Binding => (reference(ahb_bus)) applies to B3;
		
		Actual_Connection_Binding => (reference(spi_bus)) applies to B4;
		Actual_Connection_Binding => (reference(spi_bus)) applies to B5;
		Actual_Connection_Binding => (reference(spi_bus)) applies to B6;

		
		Actual_Memory_Binding => (reference(flash_mem), reference(sram_mem)) applies to main_process, 
														main_process.buzzer_thread, 
														main_process.led_thread, 
														main_process.mqtt_thread;											
		
		SEI::BandWidthBudget => 50.0 MBytesps applies to ahb_bus;
		SEI::BandWidthBudget => 2.0 KBytesps applies to spi_bus;
		
		SEI::BandWidthBudget => 10.0 MBytesps applies to B1;
		SEI::BandWidthBudget => 5.0 MBytesps applies to B2;
		SEI::BandWidthBudget => 10.0 MBytesps applies to B3;	
		
		SEI::BandWidthBudget => 0.5 KBytesps applies to B4;
		SEI::BandWidthBudget => 0.1 KBytesps applies to B5;
		SEI::BandWidthBudget => 0.1 KBytesps applies to B6;


		SEI::MIPSBudget => 10.0 kips applies to co2_sensor;
		SEI::RAMBudget => 2.0 KByte applies to co2_sensor;
		SEI::ROMBudget => 0.0 KByte applies to co2_sensor;
		
		SEI::MIPSBudget => 10.0 kips applies to buzzer;
		SEI::RAMBudget => 2.0 KByte applies to buzzer;
		SEI::ROMBudget => 0.0 KByte applies to buzzer;
		
		SEI::MIPSBudget => 10.0 kips applies to led;
		SEI::RAMBudget => 2.0 KByte applies to led;
		SEI::ROMBudget => 0.0 KByte applies to led;
		
	----------- POWER PROPERTIES -----------------
		SEI::PowerCapacity => 200.0 mW;
			

end STM32_Controller_System.impl;

--------------------- PROCESS -----------------------

process MainProcess
	features
		co2_data_in : in data port CO2Data;
		buzzer_sig : out data port TurnOnOffType;
		led_sig : out data port TurnOnOffType;
		measured_data : out data port MeasuredData;
	flows
		buzzer_flow : flow path co2_data_in -> buzzer_sig;
		led_flow : flow path co2_data_in -> led_sig;
	properties
		SEI::RAMBudget => 16.0 KByte;
		SEI::ROMBudget => 8.0 KByte;
		SEI::MIPSBudget => 0.8 mips;

		
end MainProcess;

process implementation MainProcess.with_thread
	subcomponents
		buzzer_thread : thread BuzzerTask;
		led_thread : thread LedTask;
		mqtt_thread : thread MqttTask;
	connections
		CC1: port co2_data_in -> buzzer_thread.co2_data_in;
		CC2: port co2_data_in -> mqtt_thread.co2_data_in;
		CC3: port co2_data_in -> led_thread.co2_data_in;
		CC4 : port buzzer_thread.buzzer_sig -> buzzer_sig;
		CC5 : port led_thread.led_sig -> led_sig;
		CC6 : port mqtt_thread.measured_data -> measured_data;
	flows
		buzzer_flow : flow path co2_data_in -> CC1 -> buzzer_thread.buzzer_flow -> CC4 -> buzzer_sig;
		led_flow : flow path co2_data_in -> CC3 -> led_thread.led_flow -> CC5 -> led_sig;
		
	properties
		SEI::MIPSBudget => 0.2 mips applies to buzzer_thread;
		SEI::MIPSBudget => 0.2 mips applies to led_thread;
		SEI::MIPSBudget => 0.2 mips applies to mqtt_thread;
		
		
		SEI::RAMBudget => 4.0 KByte applies to buzzer_thread;
		SEI::RAMBudget => 4.0 KByte applies to led_thread;
		SEI::RAMBudget => 8.0 KByte applies to mqtt_thread;
		
		SEI::ROMBudget => 2.0 KByte applies to buzzer_thread;
		SEI::ROMBudget => 2.0 KByte applies to led_thread;
		SEI::ROMBudget => 3.0 KByte applies to mqtt_thread;

end MainProcess.with_thread;


---------------------- THREADS ------------------------

thread BuzzerTask
	features
		co2_data_in : in data port CO2Data;
		buzzer_sig : out data port TurnOnOffType;
	flows
		buzzer_flow : flow path co2_data_in -> buzzer_sig;
end BuzzerTask;

thread LedTask
	features
		co2_data_in : in data port CO2Data;
		led_sig : out data port TurnOnOffType;
	flows
		led_flow : flow path co2_data_in -> led_sig;
end LedTask;

thread MqttTask
	features
		co2_data_in : in data port CO2Data;
		measured_data : out data port MeasuredData;
	flows
		mqtt_flow : flow path co2_data_in -> measured_data;
end MqttTask;


--=================== HARDWARE =========================

-------------------- PROCESSOR -------------------------

processor ARM_CortexM
	features
		ahb : requires bus access AHB;
	properties
		SEI::MIPSCapacity => 1.2 mips;
--		SEI::RAMCapacity => 32.0 KByte;
--		SEI::ROMCapacity => 16.0 KByte;
end ARM_CortexM;

-------------------- BUS ------------------------------

bus AHB
	properties
		Transmission_Time => [Fixed => 1ns..2ns; PerByte => 2ns..3ns;];
		Period => 1 ms;
		SEI::BandWidthCapacity => 1.2 GBytesps;
end AHB;

bus SPI
	properties
		Period => 1ms;
		SEI::BandwidthCapacity => 10.0 KBytesps;
end SPI;

-------------------- MEMORY --------------------------

memory internalMemory
	features
		ahb: requires bus access AHB;
end internalMemory;

memory implementation InternalMemory.Sram
	properties
		SEI::RAMCapacity => 64.0 KByte;
end InternalMemory.Sram;

memory implementation InternalMemory.Flash
	properties
		SEI::ROMCapacity => 32.0 KByte;
end InternalMemory.Flash;

--================= DEVICES ==========================

device CO2Sensor
	features
		co2_data_out : out data port CO2Data;
		spi : requires bus access SPI;
	flows
		co2_src : flow source co2_data_out;	
end CO2Sensor;

device Buzzer
	features
		turn_on_off : in data port TurnOnOffType;
		spi : requires bus access SPI;
	flows
		co2_sink : flow sink turn_on_off;
end Buzzer;

device Led
	features
		turn_on_off : in data port TurnOnOffType;
		spi : requires bus access SPI;
	flows
		co2_sink : flow sink turn_on_off;
end Led;

end STM32_Controller;