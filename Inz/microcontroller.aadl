package Microcontroller
public
	with DataTypes;
	renames DataTypes::all;

	with Buses;
	with SEI;
	

system Microcontroller_System
	features
		measured_data_out : out data port MeasuredData;
		wifi : requires bus access Buses::WiFI;
	flows
		mqtt_src : flow source measured_data_out;
end Microcontroller_System;

system implementation Microcontroller_System.impl
	subcomponents
		-- software
		MainProcess : process MainProcess.with_thread;

		-- hardware ---
		STM32 : processor STM32;
		Ahb_Bus : bus Buses::AHB;
		Spi_Bus : bus Buses::SPI;
		Flash_Mem : memory internalMemory.Flash;
		Sram_Mem : memory internalMemory.Sram;
		Co2_Sensor : device CO2Sensor;
		Buzzer : device Buzzer;
		Led: device Led;
				
	connections
		C1 : port Co2_Sensor.co2_data -> MainProcess.Co2_Data;
		C2 : port MainProcess.Buzzer_Sig -> Buzzer.turn_on_off;
		C3 : port MainProcess.Led_Sig -> Led.turn_on_off;
		C4 : port MainProcess.Measured_Data -> measured_data_out;
		
		B1 : bus access Ahb_Bus <-> STM32.ahb;
		B2 : bus access Ahb_Bus <-> Flash_Mem.ahb;
		B3 : bus access Ahb_Bus <-> Sram_Mem.ahb;
		
		B4 : bus access Spi_Bus <-> Co2_Sensor.spi;
		B5 : bus access Spi_Bus <-> Buzzer.spi;
		B6 : bus access Spi_Bus <-> Led.spi;
										
	flows
		F1: end to end flow Co2_Sensor.co2_src -> 
										C1 ->
										MainProcess.FF1 ->
										C2 ->
										Buzzer.co2_sink;
										
		F2: end to end flow Co2_Sensor.co2_src ->
									C1 ->
									MainProcess.FF2 ->
									C3 ->
									Led.co2_sink;																					
	properties
		Actual_Processor_Binding => (reference(STM32)) applies to MainProcess.Buzzer_Thread;
		Actual_Processor_Binding => (reference(STM32)) applies to MainProcess.Led_Thread;
		Actual_Processor_Binding => (reference(STM32)) applies to MainProcess.Mqtt_Thread;
		
		Actual_Memory_Binding => (reference(STM32)) applies to Flash_Mem;
		Actual_Memory_Binding => (reference(STM32)) applies to Sram_Mem;	
		
		Actual_Connection_Binding => (reference (Ahb_Bus)) applies to B1;
		Actual_Connection_Binding => (reference(Ahb_Bus)) applies to B2;
		Actual_Connection_Binding => (reference(Ahb_Bus)) applies to B3;
		
		Actual_Connection_Binding => (reference(Spi_Bus)) applies to B4;
		Actual_Connection_Binding => (reference(Spi_Bus)) applies to B5;
		Actual_Connection_Binding => (reference(Spi_Bus)) applies to B6;

		
		Actual_Memory_Binding => (reference(Flash_Mem), reference(Sram_Mem)) applies to MainProcess, 
														MainProcess.Buzzer_Thread, 
														MainProcess.Led_Thread, 
														MainProcess.Mqtt_Thread;											
		
		SEI::BandWidthBudget => 50.0 MBytesps applies to Ahb_Bus;
		SEI::BandWidthBudget => 2.0 KBytesps applies to Spi_Bus;
		
		SEI::BandWidthBudget => 10.0 MBytesps applies to B1;
		SEI::BandWidthBudget => 5.0 MBytesps applies to B2;
		SEI::BandWidthBudget => 10.0 MBytesps applies to B3;	
		
		SEI::BandWidthBudget => 0.5 KBytesps applies to B4;
		SEI::BandWidthBudget => 0.1 KBytesps applies to B5;
		SEI::BandWidthBudget => 0.1 KBytesps applies to B6;


		SEI::MIPSBudget => 10.0 kips applies to Co2_Sensor;
		SEI::RAMBudget => 2.0 KByte applies to Co2_Sensor;
		SEI::ROMBudget => 0.0 KByte applies to Co2_Sensor;
		
		SEI::MIPSBudget => 10.0 kips applies to Buzzer;
		SEI::RAMBudget => 2.0 KByte applies to Buzzer;
		SEI::ROMBudget => 0.0 KByte applies to Buzzer;
		
		SEI::MIPSBudget => 10.0 kips applies to Led;
		SEI::RAMBudget => 2.0 KByte applies to Led;
		SEI::ROMBudget => 0.0 KByte applies to Led;
		
	----------- POWER PROPERTIES -----------------
		SEI::PowerCapacity => 200.0 mW;
			

end Microcontroller_System.impl;

--------------------- PROCESS -----------------------

process MainProcess
	features
		Co2_Data : in data port CO2Data;
		Buzzer_Sig : out data port TurnOnOffType;
		Led_Sig : out data port TurnOnOffType;
		Measured_Data : out data port MeasuredData;
	flows
		FF1 : flow path Co2_Data -> Buzzer_Sig;
		FF2 : flow path Co2_Data -> Led_Sig;
		FF3 : flow path Co2_Data -> Measured_Data;
	properties
		SEI::RAMBudget => 16.0 KByte;
		SEI::ROMBudget => 8.0 KByte;
		SEI::MIPSBudget => 0.8 mips;
		
end MainProcess;

process implementation MainProcess.with_thread
	subcomponents
		Buzzer_Thread : thread BuzzerTask;
		Led_Thread : thread LedTask;
		Mqtt_Thread : thread MqttTask;
	connections
		CC1: port Co2_Data -> Buzzer_Thread.Co2_Data;
		CC2: port Co2_Data -> Mqtt_Thread.Co2_Data;
		CC3: port Co2_Data -> Led_Thread.Co2_Data;
		CC4 : port Buzzer_Thread.Buzzer_Sig -> Buzzer_Sig;
		CC5 : port Led_Thread.Led_Sig -> Led_Sig;
		CC6 : port Mqtt_Thread.Measured_Data -> Measured_Data;
	flows
		FF1 : flow path Co2_Data -> CC1 -> Buzzer_Thread.Buzzer_Flow -> CC4 -> Buzzer_Sig;
		FF2 : flow path Co2_Data -> CC3 -> Led_Thread.Led_Flow -> CC5 -> Led_Sig;
		FF3 : flow path Co2_Data -> CC2 -> Mqtt_Thread.Mqtt_Flow -> CC6 -> Measured_Data;
		
	properties
		SEI::MIPSBudget => 0.2 mips applies to Buzzer_Thread;
		SEI::MIPSBudget => 0.2 mips applies to Led_Thread;
		SEI::MIPSBudget => 0.2 mips applies to Mqtt_Thread;
		
		
		SEI::RAMBudget => 4.0 KByte applies to Buzzer_Thread;
		SEI::RAMBudget => 4.0 KByte applies to Led_Thread;
		SEI::RAMBudget => 8.0 KByte applies to Mqtt_Thread;
		
		SEI::ROMBudget => 2.0 KByte applies to Buzzer_Thread;
		SEI::ROMBudget => 2.0 KByte applies to Led_Thread;
		SEI::ROMBudget => 3.0 KByte applies to Mqtt_Thread;

end MainProcess.with_thread;


---------------------- THREADS ------------------------

thread BuzzerTask
	features
		Co2_Data : in data port CO2Data;
		Buzzer_Sig : out data port TurnOnOffType;
	flows
		Buzzer_Flow : flow path Co2_Data -> Buzzer_Sig;
	properties
		Priority => 1;
    	Dispatch_Protocol => Sporadic;
    	Period => 1000 us;
    	Compute_Execution_Time => 10 us .. 50 us;
end BuzzerTask;

thread LedTask
	features
		Co2_Data : in data port CO2Data;
		Led_Sig : out data port TurnOnOffType;
	flows
		Led_Flow : flow path Co2_Data -> Led_Sig;
	properties
		Priority => 1;
    	Dispatch_Protocol => Sporadic;
    	Period => 1000 us;
    	Compute_Execution_Time => 10 us .. 50 us;
end LedTask;

thread MqttTask
	features
		Co2_Data : in data port CO2Data;
		Measured_Data : out data port MeasuredData;
	flows
		Mqtt_Flow : flow path Co2_Data -> Measured_Data;
		
	properties
		Priority => 1;
    	Dispatch_Protocol => Sporadic;
    	Period => 1000 us;
    	Compute_Execution_Time => 20 us .. 100 us;
end MqttTask;


--=================== HARDWARE =========================

-------------------- PROCESSOR -------------------------

processor STM32
	features
		ahb : requires bus access Buses::AHB;
	properties
		SEI::MIPSCapacity => 1.2 mips;
end STM32;

-------------------- MEMORY --------------------------

memory internalMemory
	features
		ahb: requires bus access Buses::AHB;
end internalMemory;

memory implementation internalMemory.Sram
	properties
		SEI::RAMCapacity => 64.0 KByte;
end InternalMemory.Sram;

memory implementation internalMemory.Flash
	properties
		SEI::ROMCapacity => 32.0 KByte;
end InternalMemory.Flash;

--================= DEVICES ==========================

device CO2Sensor
	features
		co2_data : out data port CO2Data;
		spi : requires bus access Buses::SPI;
	flows
		co2_src : flow source co2_data;	
end CO2Sensor;

device Buzzer
	features
		turn_on_off : in data port TurnOnOffType;
		spi : requires bus access Buses::SPI;
	flows
		co2_sink : flow sink turn_on_off;
end Buzzer;

device Led
	features
		turn_on_off : in data port TurnOnOffType;
		spi : requires bus access Buses::SPI;
	flows
		co2_sink : flow sink turn_on_off;
end Led;

end Microcontroller;