package Server
public
	with DataTypes;	
	renames DataTypes::all;
	
	with Buses;
	with SEI;
	
	system ServerSystem
		features
			data_in : in data port MeasuredData;
			wifi : requires bus access Buses::WiFi;
		flows
			mqtt_sink : flow sink data_in;
	end ServerSystem;
	
	system implementation ServerSystem.impl
		subcomponents
			mqtt_server : system MqttServerSystem;
			database : system DatabaseSystem;
			ethernet : bus Buses::Ethernet;
			cpu : processor EthProcessor;
			
		connections
			C1 : port data_in -> mqtt_server.data_in;
			C2: port mqtt_server.data_out -> database.data_in;
			
			B1: bus access ethernet <-> mqtt_server.eth;
			B2 : bus access ethernet <-> database.eth;
			
			B3 : bus access ethernet <-> cpu.eth;
			
		flows
			e2e_mqtt_db : end to end flow mqtt_server.mqtt_src -> C2 -> database.data_sink;
	    properties
        	Actual_Connection_Binding => (reference(ethernet)) applies to C2, B3;
			Actual_Processor_Binding => (reference(cpu)) applies to mqtt_server, database;
			
			SEI::BandWidthCapacity => 10.0 GBytesps applies to ethernet;
			SEI::BandWidthBudget => 2.0 GBytesps applies to C2;
			
	end ServerSystem.impl;
	
	system MqttServerSystem
		features
			data_in : in data port MeasuredData;
			data_out : out data port ProcessedData;
			eth : requires bus access Buses::Ethernet;
		flows
			mqtt_sink : flow sink data_in;
			mqtt_src : flow source data_out;
	end MqttServerSystem;
	
	
	system DatabaseSystem
		features
			data_in : in data port DataTypes::ProcessedData;			
			eth : requires bus access Buses::Ethernet;
		flows
			data_sink : flow sink data_in;
	end DatabaseSystem;
	
	processor EthProcessor
		features
			eth : requires bus access Buses::Ethernet;
	end EthProcessor;
	
	
end Server;
